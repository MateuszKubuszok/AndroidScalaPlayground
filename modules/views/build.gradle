buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:1.3.1'
    classpath "jp.leafytree.gradle:gradle-android-scala-plugin:1.4"
  }
}

apply plugin: 'com.android.library'
apply plugin: "jp.leafytree.android-scala"

repositories {
  mavenCentral()
  maven { url 'https://oss.sonatype.org/content/repositories/public' }
  maven { url 'https://repo.typesafe.com/typesafe/releases' }
  jcenter()
}

android {
  compileSdkVersion 23
  buildToolsVersion "22.0.1"

  defaultConfig {
    multiDexEnabled true
    testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
  }

  // To reduce compilation time (multidex works fastest with 21)
  productFlavors {
    dev { minSdkVersion 21 }
    prod { minSdkVersion 23 }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}


tasks.withType(ScalaCompile) {
  scalaCompileOptions.useCompileDaemon = true
  scalaCompileOptions.deprecation = false
  scalaCompileOptions.additionalParameters = [
    "-unchecked",
    "-deprecation",
    "-feature",
    "-language:existentials",
    "-language:higherKinds",
    "-language:implicitConversions",
    "-language:postfixOps",
    "-Ywarn-dead-code",
    "-Ywarn-infer-any",
    "-Ywarn-unused-import",
    "-Xfatal-warnings",
    "-Xlint"
  ]
}

dependencies {
  def sdkPrefix = "com.android.support"
  def sdkVersion = "23.1.1"

  compile fileTree(dir: 'libs', include: ['*.jar'])

  // android
  compile "${sdkPrefix}:appcompat-v7:${sdkVersion}"
  compile "${sdkPrefix}:design:${sdkVersion}"
  compile "${sdkPrefix}:multidex:1.0.1"
  compile "${sdkPrefix}:support-v4:${sdkVersion}"

  // scala
  compile 'org.scala-lang:scala-library:2.11.7'
  compile 'org.scaloid:scaloid_2.11:4.1'

  // facebook
  compile 'com.facebook.android:facebook-android-sdk:4.8.2'

  // tests
  androidTestCompile "com.android.support:multidex-instrumentation:1.0.1", {
    exclude module: "multidex"
  }
}
